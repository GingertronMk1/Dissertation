\title{Development Report}
\author{
  \textbf{Author}\\
  Jack Ellis \\
  psyje5@nottingham.ac.uk\\
  4262333\\\\
  \textbf{Supervisor}\\
  Natasa Milic-Frayling\\
  psznm@nottingham.ac.uk
}
\date{}
\documentclass[12pt, a4paper]{report}
\usepackage{graphicx}
\usepackage{listings}
\usepackage{mathtools}
\lstset{
  basicstyle=\ttfamily\small,
  language=Haskell
}

\graphicspath{ {Images/} }
\begin{document}
\maketitle

\tableofcontents
\pagebreak

\section{Drawing A Window}

The first stage of development for the game is to draw a window.
Per the specifications set out in my interim report, this window is to be 640 pixels wide by 480 pixels tall.
I first created a file called \verb|Main.hs|; this can be thought of as analagous to the \verb|main| function often found in imperative languages as the "point of entry" for a given program.
Here I first declared it as a module called \verb|Main|, and then declared the \verb|main| function itself.
Again similar to an imperative language, this is the function that will be called once the program is compiled and run.
It is a function of type \verb|IO()|, meaning that ultimately all it does is perform some \verb|IO| action, some action with a side effect; it takes no arguments and returns no value that can be passed into another function.
These functions often consist of multiple commands chained in a manner that could look imperative to some however, as we shall see, this is not necessarily the case.
To begin with we must initialise GLUT; this allows it to begin working and set up a session with the window system in use.
The function \verb|getArgsAndInitialize| also returns a tuple, with a string representing argument 0 (the program name) as the first element, and a list of strings representing the remaining arguments as the second.
To again liken it to an imperative language, this is the \verb|(int argc, char* argv)| seen so often in C and C-style \verb|main| functions.

\par

Now that the window session is initialised we must provide it with some parameters with which to draw a window.
In my motivation for doing this I said that Haskell does not provide users with the ability to declare global variables.
The GLUT library however does, via a module called \verb|StateVar|.
The library has a number of such \verb|StateVar| variables, many of which will be explored as this program becomes more and more complex, but for the moment the one that is of use to us is the \verb|initialWindowSize| variable.
This is of type \verb|StateVar Size|, where \verb|Size| is a type defined in the GLUT library as little more than a wrapper for two numbers - a more readily understood tuple.
We can use the \verb|$=| operator as a "setter" for \verb|StateVar| variables, and with it we can set the window's initial size to 640x480 pixels.

\par

With the required settings now correct we can call \verb|createWindow|, passing the string "Frogger" as the window's title, and the program should compile and run.
It does not, however, because we are missing something.
If we try to compile the code we get the following error:

\begin{lstlisting}[language=sh, xleftmargin=-0.1\textwidth, xrightmargin=-0.1\textwidth]
GLUT Warning: The following is a new check for GLUT 3.0; update your code.
GLUT Fatal Error: redisplay needed for window 1, but no display callback.
\end{lstlisting}

We need to set a \verb|DisplayCallback|, essentially a function describing how to draw things within the created window at each refresh.
In order to keep things clean this function will be declared in its own module, \verb|Display|.
Currently we're only interested in drawing the window in the first place, so we can just tell it to first clear the \verb|ColorBuffer| - setting the whole content of the window to black - and then \verb|flush|, which empties all command buffers, executing the commands as quickly as possible so as to ensure the image is as expected by the user.

\begin{lstlisting}
-- In Main.hs
module Main where

main :: IO ()
main = do (_progName, _args) <- getArgsAndInitialize
          initialWindowSize $= Size 640 480
          createWindow "Frogger"
          displayCallBack $= display
          mainLoop

-- In Display.hs
module Display where

import Graphics.UI.GLUT

display :: DisplayCallback
display = do clear [ColorBuffer]
             flush
\end{lstlisting}

\par

\begin{figure}[ht]
  \centering
  \caption{The blank window drawn.}
  \includegraphics[width=0.5\textwidth]{DrawingAWindow.png}
  \label{fig:drawingawindow}
\end{figure}

With that done, we now have a window being drawn!
It is however just a blank, black-filled window, as seen in Figure \ref{fig:drawingawindow}.
In the next section we will add the game's titular \textit{Frogger}.

\section{Adding A Character}

Now that we have a window to draw in, we should draw something in it.
How better to start than with the hero of the game, the titular \textit{Frogger}?
Let's first represent him as a green unit square, just to get to grips with the coordinate and scaling system in Haskell's GLUT library.

\par

First we need to set the current drawing colour to green, which is easily done using GLUT's \verb|color| function.

\begin{lstlisting}
color $ Color3 0.0 1.0 0.0
\end{lstlisting}

This function sets the current drawing colour to whatever is passed into it, which in our case is a \verb|Color3| type (similar to the \verb|Size| type described earlier), with the 3 values representing red, green, and blue values.
Worth noting is that in Haskell anything that follows a lone \$ is thought of as being in parentheses, so the above is semantically identical to this:

\begin{lstlisting}
color (Color3 0.0 1.0 0.0)
\end{lstlisting}

Now that we have the correct colour selected we can draw a quadrilateral, which we can do with the \verb|renderPrimitive Quads| function, as follows:

\begin{lstlisting}[xleftmargin=-0.1\textwidth, xrightmargin=-0.1\textwidth]
let p = [(1,0,0),(1,1,0),(0,1,0),(0,0,0)] :: [(Float,Float,Float)]
renderPrimitive Quads $ mapM_ (\(x,y,z) -> vertex $ Vertex3 x y z) p
\end{lstlisting}

Let's unpack this a little bit.
The \verb|let| command is more or less the same as a variable declaration on the "global" scope, like declaring a variable \verb|myVar :: Int| with a value of \verb|5|, only limited to the scope of the function in which it is declared.
It is a local variable, essentially.
\verb|p| is a list of tuples, each containing 3 \verb|Float| values.
These values are the x, y, and z coordinates for each of the points of the quadrilateral we're drawing, respectively.
The reason their type is declared explicitly here is that otherwise the compiler throws an error relating to ambiguous types, and because we don't want to clog up the rest of the program with values that no other function is ever going to need to know exists we append to the "local variable declaration" an explicit type declaration.
The coordinates described by \verb|p| denote a unit square extending from what would be considered the origin on a mathematical graph in positive x and y.
The next line is the fun one, let's go through it piece by piece.
\verb|renderPrimitive| is the foundation for this; it does the actual drawing of what is fed into it.
\verb|Quads| is one of the options for "ways to draw", it means draw the following as a closed quadrilateral or series or separate closed quadrilaterals.
Other options here include drawing something as a wire frame, triangles, a single polygon, or just points.
The argument passed in here is a version of \verb|p| that has had a lambda function\footnote{lambda functions are anonymous functions written in the style of the lambda calculus. They are convenient when you need to do something slightly complicated but that will only ever get used in that one place.} mapped over it.
This lambda function takes a tuple of three values and, using two of the GLUT library's provided data constructors, converts them into a type accepted by \verb|renderPrimitive|.

\par

So with all of these in place we should have a unit square drawn nicely on the screen, right?
Let's see.

\begin{figure}[ht]
  \centering
  \caption{A "unit square" - this is a result of the way in which GLUT draws and populates a window by default.}
  \includegraphics[width=0.5\textwidth]{DrawingACharacter.png}
  \label{fig:drawingacharacter}
\end{figure}

Oh dear.
As we can see in Figure \ref{fig:drawingacharacter} this square is much too large, and begins centered on the screen, rather than how it should be - in my opinion - specifically starting at the bottom left of the window.
Conveniently GLUT provides functions for both scaling and translating, so this shouldn't be a problem!

\par

Something worth noting before we continue is that it is very important to scale and translate in the correct order; in general translation should be done first, because scaling actually affects translation.
Mathematically speaking they are not commutative actions, this is because they are both examples of matrix multiplication.

\par

The translation and scaling should be done before any other part of the display, given that they directly impact all other parts of drawing.
They are both one-line functions taking similar forms:

\begin{lstlisting}
translate $ Vector3 (-1.0) (-1.0) 0.0
scale (1.0/32.0) (1.0/24.0) 0.0
\end{lstlisting}

The main difference is that \verb|translate| takes a \verb|Vector3| data type as an argument, whereas \verb|scale| takes only 3 \verb|Float|s.
\verb|translate| moves the drawing "origin" in a way described by the coordinates in its argument; before any scaling happens a GLUT window essentially consists of 4 "1x1" quadrants (these are stretched to fill the aspect ratio of the window they're in).
The \verb|scale| function scales everything drawn after it is called by a factor of the first argument in x, second argument in y, and third argument in z.
Here the scale factors are \verb|1/32| in x and \verb|1/24| in y, these are both based on the window size in their respective direction and, because the window is effectively 2 units both across and tall, it should be scaled to $2/$ that value, and the fractions have been reduced manually.
Here one unit size is equivalent to 10 pixels, and so now we have a unit square as shown in Figure \ref{fig:drawingacharacterproperly}

\begin{figure}[ht]
  \centering
  \caption{The unit square denoting our \textit{Frogger} is now an appropriate size.}
  \includegraphics[width=0.5\textwidth]{DrawingACharacterProperly.png}
  \label{fig:drawingacharacterproperly}
\end{figure}

\begin{lstlisting}[xleftmargin=-0.1\textwidth, xrightmargin=-0.1\textwidth]
display :: DisplayCallback
display
  = do clear [ColorBuffer]
       translate $ Vector3 (-1.0) (-1.0) 0.0
       scale (1.0/32.0) (1.0/24.0) 0.0
       color $ Color3 0.0 1.0 0.0
       let p = [(1,0,0),(1,1,0),(0,1,0),(0,0,0)]
       renderPrimitive Quads $ mapM_ (\(x,y,z) -> vertex $ Vertex3 x y z) p
       flush
\end{lstlisting}

Now we have a character drawn and scaled appropriately.
Next let's give him some movement, by way of keyboard input.

\section{Keyboard Input}

Keyboard input is handled by way of another callback in the Haskell GLUT system, however implementing it will require some additional plumbing being added first.
Specifically it will require the introduction of one of the major building blocks of this program, the \verb|IORef|.
This is Haskell's way of making use of the IO Monad to do things that it, as a language, would rather not, in this instance global variables and states.
Firstly however we must declare what kind of variable it is we want to be global.
We do that in a new file, \verb|Type.hs|, which will be the point from which we declare all new types and functions relating to them.
For now we will only have one type, the \verb|Mover|, based on the \verb|MovingEntity| superclass in the GitHub project reported on elsewhere.

\begin{lstlisting}
data Mover = Frogger {x :: Float,
                      y :: Float
                     }
\end{lstlisting}

This is a "record-style" data type with two fields: x and y, denoting the x and y coordinates of the \textit{Frogger}.
With this in place we can add the \verb|Type| module to \verb|Main| and, after creating the window, create a new \verb|IORef Mover| with \verb|x| and \verb|y| both set to 0.0.

\begin{lstlisting}
f <- newIORef $ Frogger {x = 0.0, y = 0.0}
\end{lstlisting}

Finally before we set up the \verb|keyboardMouseCallback|, we modify the \verb|display| function to take as an argument an \verb|IORef Mover|, and draw the character based on it.
This requires that we change 3 lines of the function, resulting in:

\begin{lstlisting}[xleftmargin=-0.1\textwidth, xrightmargin=-0.1\textwidth]
display :: IORef Mover -> DisplayCallback
display m =
  do clear [ColorBuffer]
     loadIdentity
     fr <- get m
     translate $ Vector3 (-1.0) (-1.0) 0.0
     scale (1.0/320.0) (1.0/240.0) 0.0
     color $ Color3 0.0 1.0 0.0
     let p = [(x fr + 10,y fr,0),
              (x fr + 10,y fr + 10,0),
              (x fr,y fr + 10,0),
              (x fr,y fr,0)]
     renderPrimitive Quads $ mapM_ (\(x,y,z) -> vertex $ Vertex3 x y z) p
     swapBuffers
\end{lstlisting}

\verb|loadIdentity| is a very important function in all of this - it resets the modification matrix, which is to say that it resets the scaling and translation applied in the last call to the function.
If this function is forgotten the screen is simply left blank with no explanation as to why, and all of the objects are translated and scaled from the previous frame.
As well, the change from \verb|flush| to \verb|swapBuffers| is similarly important; the way the GLUT system works is with a pair of buffers (this is automatic on a Mac, but on other platforms will require modifying \verb|Main.hs| to add the line \verb|initialDisplayMode $= [DoubleBuffered]|).
This pair of buffers is swapped between constantly, with one being displayed and the other "behind" it.
The "behind" buffer is the next one to be drawn, and so is the updated version.
\verb|swapBuffer|, as the name suggests, swaps the buffers.
If \verb|flush| was not replaced there could therefore be no animation.

\par

With all of that done, we can now create the file \verb|Input.hs|, which will contain all of the functions required to deal with keyboard and mouse inputs.
In our case we want the WASD keys to be responsible for movement, which we can by defining a function \verb|input| which takes as an argument an \verb|IORef Mover| and returns a \verb|KeyboadMouseCallback|, which we will set the global \verb|keyboardMouseCallback| to in \verb|Main.hs|.
A \verb|keyboardMouseCallback| takes 5 arguments: our Mover variable, the key being pressed, that key's state (up or down), any modifier keys (Shift, Ctrl, Alt), and a position.
Our \verb|input| function can thus be described as follows.

\begin{lstlisting}[xleftmargin=-0.1\textwidth, xrightmargin=-0.1\textwidth]
input :: IORef Mover -> KeyboardMouseCallback
input m c Down _ _
  | c == (Char 'w') || c == (Char 'W') = m $~! \f -> f {y = y f + step}
  | c == (Char 'a') || c == (Char 'A') = m $~! \f -> f {x = x f - step}
  | c == (Char 's') || c == (Char 'S') = m $~! \f -> f {y = y f - step}
  | c == (Char 'd') || c == (Char 'D') = m $~! \f -> f {x = x f + step}
  | otherwise                          = return ()
  where step = 5
input _ _ _ _ _ = return ()
\end{lstlisting}

With this we now have a \textit{Frogger} who will move continuously about as long as either W, A, S, or D are being pressed.

There is an additional callback required for this all to work however: \verb|idleCallback|.
This callback deals with background events and is required for continuous animation.
It can handle background tasks such as updating the positions of non-player characters (a function that will have great use later on in this process).
For now however it will simply take the following form.

\begin{lstlisting}
idle :: IORef Mover -> IdleCallback
idle e = do e $~! id
            postRedisplay Nothing
\end{lstlisting}

\section{Other Characters}

\textit{Frogger} is a game of avoiding obstacles, so let's now add some obstacles to be avoided.
This will first require that we extend the \verb|Mover| type to include some "subclasses", in the first instance a \verb|Car| "subclass".

\begin{lstlisting}
data Mover = Frogger {x :: Float
                     ,y :: Float
                     ,s :: Float
           }
           | Car {x :: Float
                 ,y :: Float
                 ,l :: Float
                 ,w :: Float
                 ,v :: Float
           }
\end{lstlisting}

\verb|x| and \verb|y| are common with the \verb|Frogger| "subclass", and will be used with the drawing function we will define later.
\verb|l| and \verb|w| are the length and width of the car respectively; width will likely remain a constant, however length could change depending on what kinds of car obstacles we wish to add to the game.
\verb|v| is the velocity of the car, with the positive direction being left-to-right, or increasing in x.
This will be used in conjunction with a modified version of the \verb|idle| function, discussed later.


We now have to create a containing data type for both the Frogger and the obstacles, such that they can all be stored as one \verb|IORef|.
This is to be the environment variable, and in due course will contain all relevant information about the game, including scores, number of lives, time remaining, and so on.
For now though, it only need include two things: the Frogger and a list of other obstacles.
It can therefore be defined thus:

\begin{lstlisting}
data Env = E {player :: Mover
             ,enemies :: [Mover]
         }
\end{lstlisting}

We can then extend \verb|Display| to include a helper function to draw any \verb|Mover| to the screen.
This will be a two-step function, the first step being a call to \verb|preservingMatrix|, a function in GLUT that stores all of the current transformation information (in this instance the translation and scaling discussed when we first drew a character to the screen).
We can then call a function of our own definition, using pattern matching to ensure the appropriate version of it is called based on what kind of \verb|Mover| we are dealing with - it would not work if we drew the player character identically to a car.
Something to consider is that for all moving objects with a given velocity is that the positivity of that velocity denotes the direction of that object's movement.
Therefore we can use that value in conjunction with the Haskell Prelude function \verb|signum| to appropriately "rotate" the object (in reality this is a scale being used to flip the image 180 degrees).

\begin{lstlisting}
drawMover :: Mover -> IO()
drawMover m = preservingMatrix $ drawMover' m

drawMover' :: Mover -> IO()
drawMover' Frogger {x = fx, y = fy, s = fs}
  = do color $ Color3 0.0 1.0 0.0
       translate $ Vector3 fx fy 0.0
       scale fs fs 1.0
       unitSquare

drawMover' Car {x = cx, y = cy, l = cl, w = cw, v = cv}
  = do color $ Color3 1.0 0.0 0.0
       translate $ Vector3 cx cy 0.0
       scale (cl*signum cv) cw 1.0
       unitSquare

unitSquare :: IO()
unitSquare
  = let us = [(1,0,0),(1,1,0),(0,1,0),(0,0,0)]
     in (renderPrimitive Quads . mapM_ makeVertex) us

makeVertex :: (Float, Float, Float) -> IO ()
makeVertex (x,y,z) -> vertex $ Vertex3 x y z
\end{lstlisting}

Note the additional helper function \verb|unitSquare| - this will be replaced in due course as the game's art becomes more complex however for now we can use it to draw a square of size 1x1 pixel, which we can translate and scale as we wish.
The helper \verb|makeVertex| on the other hand will likely only become more useful as the complexity of drawing increases - it is a useful constructor, taking 3 points and converting them into the data type used by \verb|renderPrimitive|.

\par

Now that we have these we can extend \verb|display| to take as an argument an \verb|IORef Env| rather than an \verb|IORef Mover|, and replace much of the drawing code with the \verb|drawMover| function applied to parts of the environment variable.

\begin{lstlisting}
display :: IORef Env -> DisplayCallback
display m = do clear [ColorBuffer]
               loadIdentity
               fr <- get m
               translate $ Vector3 (-1.0) (-1.0) 0.0
               scale (1.0/320.0) (1.0/240.0) 0.0
               (drawMover . player) fr
               (sequence . map drawMover . enemies) fr
               swapBuffers
\end{lstlisting}

\begin{figure}[ht]
  \centering
  \caption{The \textit{Frogger} being closed in on by two cars}
  \includegraphics[width=0.5\textwidth]{DrawingEnemies.png}
  \label{fig:drawingenemies}
\end{figure}

As we can see in Figure \ref{fig:drawingenemies}, we can now draw cars to the screen.
By modifying the \verb|idle| function we can also animate them, and with that we can get to the first real glimpse of the game of \textit{Frogger}.

\par

The modification to \verb|idle| is slightly involved, mostly because of the now nested records we have meaning that modifying an internal one is more or less a two-step process.
Fortunately Haskell's \verb|let| syntax makes this process considerably easier, because we can take the internal record value (i.e. the one we want to change) and store it with a different name.
We can then take a helper function, whose role is to update the x position of a mover based on its velocity, and map it across the \verb|enemies| field of the environment variable, thus:

\begin{lstlisting}
idle :: IORef Env -> IdleCallback
idle e = do e $~! \env -> let es = enemies env
                          in env {enemies = map updateMover es}
            postRedisplay Nothing

updateMover :: Mover -> Mover
updateMover c@(Car {x = cx, v = cv}) = c {x = cx + cv}
\end{lstlisting}

This two-step processing does also have to be extended to the \verb|input| functions, which leads to a great deal of boilerplate code.

\begin{lstlisting}
input :: IORef Env -> KeyboardMouseCallback
input m c Down _ _
  | c == (Char 'w') || c == (Char 'W')
    = m $~! \e -> let p = player e
                  in e {player = p {y = y p + step}}
  | c == (Char 'a') || c == (Char 'A')
    = m $~! \e -> let p = player e
                  in e {player = p {x = x p - step}}
  | c == (Char 's') || c == (Char 'S')
    = m $~! \e -> let p = player e
                  in e {player = p {y = y p - step}}
  | c == (Char 'd') || c == (Char 'D')
    = m $~! \e -> let p = player e
                  in e {player = p {x = x p + step}}
  | otherwise
    = return ()
  where step = 5
input _ _ _ _ _ = return ()
\end{lstlisting}

The repetition of \verb|let p = player e in e {...| throughout the middle of those guarded functions is both ugly and not especially useful; if that function were to change each individual instance of it would have to, and the more of them that there are, the more likelihood one will be missed.
This has major negative impact on the maintainability of this code, and will have to be addressed.
For now however, we are focussed on getting the game to a point where it is playable.

\section{Collision Detection}

This will again consist mostly of modifications to the \verb|idle| function - at this stage in development any kind of detection is the desired goal; acting upon this detection is a task for later.
The function \verb|hasCollided| will do this; it will take as an input two \verb|Mover|s (one being the \verb|Frogger|) and, taking into account the direction of the enemy mover, will return whether or not their two hitboxes intersect.
If they do, the \textit{Frogger} will die.

\begin{lstlisting}
hasCollided :: Mover -> Mover -> Bool
hasCollided f m
  = case signum (l m) of 1  -> x m + l m > x f &&
                               y m + w m > y f &&
                               x f + s f > x m &&
                               y f + s f > y m
                         -1 -> x m > x f &&
                               y m + w m > y f &&
                               x f + s f > x m + l m &&
                               y f + s f > y m
                         otherwise -> False
\end{lstlisting}

This function simply determines whether or not any combination of the top/bottom and left/right boundaries of the \textit{Frogger} intersect those of the \verb|Mover| it is being checked against.
We can then map this function over the \verb|enemies| field of the environment, checking the \textit{Frogger} against every enemy.
We can then create a function \verb|hitCheck| to do exactly that, returning a list of \verb|Bool|s, which we can apply \verb|or| over to determine if any enemy has hit the \textit{Frogger}.
If any have, at the moment it simply prints "Hit!", otherwise does nothing.

\begin{lstlisting}
hitCheck :: Env -> IO ()
hitCheck env = if or $ map (hasCollided (player env)) (enemies env)
                  then putStrLn "Hit!"
               else return ()
\end{lstlisting}

As it stands I am pleased that simple rectangular hitboxes are an adequate option.

\section{Game State}

Next we add the ability to play/pause the game, or rather add a game state variable in general.
This will allow for not only the aforementioned playing and pausing of the game but also for things such as splash screens and death events to be reflected more appropriately.
To do this is first a matter of modifying \verb|Type.hs|, firstly creating a new type \verb|GameState| which will give us a verbose manner of specifying what state the game is in.
This could be done using a simple integer, with 1 representing playing, 2 paused, and so on, but Haskell provides us with the tools to do so in a more intuitive and easier to follow manner, so we shall use them.

\begin{lstlisting}
data GameState = PreStart | Playing | Paused | PlayerDead
                 deriving (Eq, Show)
\end{lstlisting}

Now we add a field to \verb|Env| which will contain a \verb|GameState| variable, and that's it.
Not quite, of course, we still have to do the relevant plumbing to make sure things are actually playing and pausing as they should.

\par

We can begin in \verb|Idle|, where all of the updating of the objects on the screen happens.
Here we can add a check, an if statement querying the current state of the game as given in the argument.
If the game is currently in the \verb|Playing| state we can continue to update everything as normal.
Otherwise pause all updating, using \verb|return()| as the empty case.

\begin{lstlisting}
idle e = do e' <- get e
            if gameState e' == Playing
            then do e $~! \env -> let es = enemies env
                                  in env {enemies = map updateMover es}
            else return ()
            postRedisplay Nothing
\end{lstlisting}

To move between the states we must update \verb|Input.hs|, adding cases for the various new \verb|GameState|s.

\begin{lstlisting}
input :: IORef Env -> KeyboardMouseCallback
input m k ks mo p
  = do m' <- get m
       case (gameState m') of Playing   -> inputPlaying m k ks mo p
                              Paused    -> inputPaused m k ks mo p
                              otherwise -> return ()

inputPaused :: IORef Env -> KeyboardMouseCallback
inputPaused m c Down _ _
  | c == (Char ' ') = m $~! \e -> e {gameState = Playing}
  | otherwise       = return ()
inputPaused _ _ _ _ _ = return ()

inputPlaying :: IORef Env -> KeyboardMouseCallback
inputPlaying m c Down _ _
  | c == (Char 'w') || c == (Char 'W')
      = m $~! \e -> let p = player e
                     in e {player = p {y = y p + step}}
  | c == (Char 'a') || c == (Char 'A')
      = m $~! \e -> let p = player e
                     in e {player = p {x = x p - step}}
  | c == (Char 's') || c == (Char 'S')
      = m $~! \e -> let p = player e
                     in e {player = p {y = y p - step}}
  | c == (Char 'd') || c == (Char 'D')
      = m $~! \e -> let p = player e
                     in e {player = p {x = x p + step}}
  | c == (Char ' ')
      = m $~! \e -> e {gameState = Paused}
  | otherwise                          = return ()
  where step = 32
inputPlaying _ _ _ _ _ = return ()
\end{lstlisting}

\section{Collision Detection 2 - Detection on the River}

Collision detection on the river section is slightly more complex than it is on the road; in this case it is a matter of the player remaining alive if they \textit{have} collided with an object, and dying if they step onto a lane with no object present.

Hence this will require some modification of the code.
We introduce a custom typeclass\footnote{A typeclass in Haskell can be thought of similarly to superclasses in imperative languages.
They are collections of types which support certain operations, and allow for ad-hoc polymorphism. www.haskell.org/tutorial/classes.html}, \verb|Drawable|.
This typeclass will have two operations, \verb|draw :: a -> IO()|, with which we can replace the \verb|drawMover| function, and \verb|update :: a -> a|, with which we can replace the \verb|updateMover| function.
It will also include operations to draw the object to the screen whilst preserving the current transformation matrix, and to do so to a list of objects.
Both of the latter operations will have default definitions.

\begin{lstlisting}
class Drawable a where
  update :: a -> a
  draw :: a -> IO()
  preservingDraw :: a -> IO()
  preservingDraw = preservingMatrix . draw
  preservingDraws :: [a] -> IO [()]
  preservingDraws = sequence . map preservingDraw
\end{lstlisting}

This will allow us to break the \verb|Mover| type down, improving readability and giving us the ability to create functions for more specific instances of a type.
In particular, we can split it into 4 new types:
\begin{itemize}
  \item \verb|Frogger|: the player character.
    \item \verb|RoadMover|: moving objects on the road.
    \item \verb|RiverMover|: moving objects on the river.
    \item \verb|Goal|: the goal objects.
\end{itemize}

\par

As a result of this the \verb|Env| type must be modified with the \verb|enemies| field being split into \verb|roadEnemies|, \verb|riverEnemies|, and \verb|goals|.

As well as this, the collision detection function will be modified to take the x, y, length, and width values of both objects as arguments, as opposed to just taking the objects themselves.

\par

With this we can split the \verb|hitCheck| function into three parts:
\begin{itemize}
  \item \verb|roadCollision :: Frogger -> RoadMover -> GameState|\\
    If the Frogger has collided with the road object, it returns \verb|PlayerDead|, otherwise it returns \verb|Playing|.
    We can use this to build a function \verb|hitCheckRoad|,
    \begin{lstlisting}
      hitCheckRoad :: Frogger -> [RoadMover] -> GameState
      hitCheckRoad f rs
        = let cs = map (roadCollision f) rs
          in hcro' collisions
          where hcro' [] = Playing
                hcro' (s:ss) = if s == PlayerDead then s
                                                  else hcro' ss
    \end{lstlisting}
  \item \verb|riverCollision :: Frogger -> RiverMover -> GameState|\\
    Essentially the opposite: if there is a collision it will return \verb|Playing|, else \verb|Playing|.
    \begin{lstlisting}
      hitCheckRiver :: Frogger -> [RoadMover] -> GameState
      hitCheckRiver f rs
        = let cs = map (riverCollision f) rs
           in hcri' collisions
          where hcri' [] = PlayerDead
                hcri' (s:ss) = if s /= PlayerDead then s
                                                  else hcri' ss
    \end{lstlisting}
  \item \verb|goalCollision :: Frogger -> Goal -> Bool|\\
    A simple boolean check to see if a goal has been collided with.
    This function can then be used in a larger function \verb|hitCheckGoal|, which at this point simply \verb|or|s the list of collided goals, and returns \verb|LevelComplete| if that is \verb|True|.
    \begin{lstlisting}
      hitCheckGoals f gs
        = let cs = map (goalCollision f) rs
           in if or cs then LevelComplete
                       else Playing
    \end{lstlisting}
\end{itemize}

\verb|hitCheck| now becomes a "routing" function, calling \verb|roadCollision|, \verb|riverCollision|, or \verb|goalCollsion| depending upon where on the map the player is, while checking that the player is within the screen bounds (if they go beyond the screen they die).

\begin{lstlisting}
hitCheck :: Env -> GameState
hitCheck e
  = let frogger = player e
        fx = f_X frogger
        fy = f_Y frogger
     in if inRange (0,initSizeX) fx && inRange (0,initSizeY) fy
        then if inRange ((head lanes),(lanes !! 4)) fy
                then hitCheckRoad frogger (roadEnemies e)
             else if inRange ((lanes !! 6),(lanes !! 11)) fy
                then hitCheckRiver frogger (riverEnemies e)
             else hitCheckGoals frogger (goals e)
        else PlayerDead
        where inRange (l,u) n = u >= n && n >= l
\end{lstlisting}

\verb|lanes| is a data object exported by \verb|Type| containing the y-values of all lanes - lanes 1-5 (index 0-4) are the road lanes, and lanes 7-10 (index 6-11) are the river lanes.

We now have support for determining death both on the road and on the river.
However if the player is sat on a log they currently do not move.
Fixing this will be the next step.

\section{Rolling on the River}

The first step to this will be updating the \verb|Frogger| data type with a velocity element, \verb|f_V|.
We then change the \verb|update| function in its \verb|Drawable| instance from the identity function to something resembling the definition for the other moving objects, specifically

\begin{lstlisting}
update f@({f_X = fx, f_V = fv}) = f {f_X = fx + fv}
\end{lstlisting}

With that done, and updating the Frogger's position added to the idle callback, we can implement the "follow" functionality.

\par

To do this we make use of Haskell's \verb|Either| type, which allows a function to return results of one of two types, given as the \verb|Left| type and \verb|Right| type.

Therefore \verb|hitCheck|'s type signature will now be 

\begin{lstlisting}
hitCheck :: Env -> Either GameState Float
\end{lstlisting}

Thanks to the breaking down of the \verb|hitCheck| function into its road, river, and goal elements we need only concern ourselves with changing the \verb|hitCheckRiver| function, simply prepending \verb|Left| to the other returned \verb|GameState|s.

All definitions for \verb|riverCollision| will be updated in the same way, but we will again use the one for \verb|Log|s as an example:

\begin{lstlisting}
riverCollision (Frogger {f_X = fx, f_Y = fy, f_S = fs}) (Log {ri_X = lx, ri_Y = ly, ri_L = ll, ri_W = lw, ri_V = lv})
  = if hasCollided fx fy fs lx ly ll lw then Right lv
                                        else Left PlayerDead

\end{lstlisting}

As we can see, the function has been updated to, in the instance of a collision, return the \verb|Right| type, a float giving the value of the Frogger's new velocity.
Importantly if a \verb|Float| is returned it can be assumed that the player is not dead.

\par

All updating of the main \verb|Env| variable has now been moved to a function \verb|updateEnv|, which is only called when the \verb|gameState| is \verb|Playing|.

Within this update is applied to all applicable fields, and the \verb|Either| returned by \verb|hitCheck| is now dealt with as follows

\begin{lstlisting}
(f_V', gameState') = case hitCheck e of Left gs -> (0.0, gs)
                                        Right v -> (v, Playing)
\end{lstlisting}

\section{Cause of death}

It would be nice if the player knew what killed them - \textit{Frogger} after all has the most ways to die of any game according to a 1982 \textit{Softline} article - so that will be the next thing implemented.

The first thing to do here is to modify the \verb|GameState| data type, adding a \verb|String| to \verb|PlayerDead| such that it can carry a message.

Then we update \verb|hitCheckRoad|, \verb|hitCheckRiver|, and their associated collision functions, which conveniently return \verb|GameState|s rather than \verb|Bool|s.
This means that we can trivially add to the \verb|PlayerDead| options with a brief message detailing what killed the player, getting hit by a car or drowning being the current options.
Finally we update the end of \verb|hitCheck| with the fact that the player has died by jumping off the map, and it's done.

\section{Final Touches}

First we update the \verb|display| function to show different things depending on the current state of the game
\begin{itemize}
  \item In \verb|PreStart| it should show a welcome message detailing the controls of the game.
  \item In \verb|PlayerDead s| it should render \verb|s|, the string explaining why as well as the number of points the player has accrued in their game.
  \item In \verb|LevelComplete| it should congratulate the player on completing the level, tell them how many points they've got, and tell them how to advance to the next level.
  \item Otherwise it should draw the map according to the current \verb|Env|
\end{itemize}

This can be accomplished by adding a \verb|case gameState e of ...| statement to \verb|display|, with the different options being applied as necessary.

\begin{lstlisting}
case (gameState fr) of PreStart      -> -- Draw a welcome message
                       PlayerDead _  -> -- Describe how the player died and their score
                       LevelComplete -> -- Show the players score
                       otherwise     -> -- Draw the game as before
\end{lstlisting}

With this addition I believe we can consider the initial version of the game complete.

\end{document}
